const std = @import("std");
const Allocator = std.mem.Allocator;

const types = @import("@zeam/types");
const xev = @import("xev").Dynamic;
const zeam_utils = @import("@zeam/utils");

const interface = @import("./interface.zig");
const NetworkInterface = interface.NetworkInterface;
const node_registry = @import("./node_registry.zig");
const NodeNameRegistry = node_registry.NodeNameRegistry;

const ZERO_HASH = types.ZERO_HASH;

pub const Mock = struct {
    allocator: Allocator,
    logger: zeam_utils.ModuleLogger,
    gossipHandler: interface.GenericGossipHandler,
    peerEventHandler: interface.PeerEventHandler,
    registry: *NodeNameRegistry,
    owns_registry: bool,

    rpcCallbacks: std.AutoHashMapUnmanaged(u64, interface.ReqRespRequestCallback),
    peerLookup: std.StringHashMapUnmanaged(usize),
    ownerToPeer: std.AutoHashMapUnmanaged(usize, usize),
    peers: std.ArrayList(Peer),
    connectedPairs: std.AutoHashMapUnmanaged(PairKey, void),
    activeStreams: std.AutoHashMapUnmanaged(u64, *MockServerStream),
    timer: xev.Timer,
    nextPeerIndex: usize,
    nextRequestId: u64,
    /// Issue #808 review: when set to true, every `publish` call returns
    /// `false` without invoking subscribers — simulating the rust-libp2p
    /// command channel having dropped the publish. Lets the node-level
    /// `failed to publish … (backend dropped publish)` warn arms in
    /// `Node.publishBlock` / `publishAttestation` / `publishAggregation` be
    /// exercised in tests without spinning up a real Rust bridge.
    force_publish_drop: bool = false,

    const Self = @This();

    const PairKey = struct {
        a: usize,
        b: usize,

        fn from(a: usize, b: usize) PairKey {
            return if (a <= b) PairKey{ .a = a, .b = b } else PairKey{ .a = b, .b = a };
        }
    };

    const Peer = struct {
        owner_key: usize,
        peer_id: ?[]u8 = null,
        req_handler: ?interface.OnReqRespRequestCbHandler = null,
        event_handler: ?interface.OnPeerEventCbHandler = null,

        fn isReady(self: *const Peer) bool {
            return self.req_handler != null and self.event_handler != null and self.peer_id != null;
        }
    };

    const StreamError = error{StreamAlreadyFinished};

    const MockServerStream = struct {
        mock: *Mock,
        request_id: u64,
        method: interface.LeanSupportedProtocol,
        sender_peer_id: []const u8,
        finished: bool = false,
        // Buffer responses for async delivery to avoid timing issues.
        // In the mock, the target handler is called synchronously within sendRequest(),
        // so responses would arrive before sendRequest() returns the request_id.
        buffered_responses: std.ArrayList(interface.ReqRespResponse) = .empty,
        error_response: ?struct { code: u32, message: []const u8 } = null,
    };

    fn mockStreamGetPeerId(ptr: *anyopaque) ?[]const u8 {
        const ctx: *MockServerStream = @ptrCast(@alignCast(ptr));
        return ctx.sender_peer_id;
    }

    const SyntheticResponseTask = struct {
        mock: *Mock,
        request_id: u64,
        method: interface.LeanSupportedProtocol,
        payload: union(enum) {
            success: interface.ReqRespResponse,
            failure: struct {
                code: u32,
                message: []const u8,
            },
        },

        fn init(mock: *Mock, request_id: u64, method: interface.LeanSupportedProtocol, request: *const interface.ReqRespRequest) !*SyntheticResponseTask {
            const task = try mock.allocator.create(SyntheticResponseTask);
            task.mock = mock;
            task.request_id = request_id;
            task.method = method;
            switch (request.*) {
                .status => |status_req| {
                    task.payload = .{ .success = interface.ReqRespResponse{ .status = status_req } };
                },
                .blocks_by_root => {
                    task.payload = .{ .failure = .{ .code = 1, .message = "mock peer has no block data" } };
                },
                .blocks_by_range => {
                    task.payload = .{ .failure = .{ .code = 1, .message = "mock peer has no block data" } };
                },
            }
            return task;
        }

        fn release(self: *SyntheticResponseTask) void {
            switch (self.payload) {
                .success => |*resp| resp.deinit(),
                .failure => {},
            }
            self.mock.allocator.destroy(self);
        }

        fn dispatch(self: *SyntheticResponseTask) void {
            switch (self.payload) {
                .success => |*resp| {
                    const mock = self.mock;
                    mock.notifySuccess(self.request_id, self.method, resp.*);
                    resp.deinit();
                    mock.notifyCompleted(self.request_id, self.method);
                },
                .failure => |err_payload| {
                    self.mock.notifyError(self.request_id, self.method, err_payload.code, err_payload.message);
                },
            }
            self.mock.allocator.destroy(self);
        }
    };

    fn syntheticResponseCallback(ud: ?*SyntheticResponseTask, _: *xev.Loop, completion: *xev.Completion, r: xev.Timer.RunError!void) xev.CallbackAction {
        _ = r catch |err| {
            if (ud) |task| {
                const mock = task.mock;
                mock.logger.err("mock:: Synthetic response scheduling failed: {any}", .{err});
                task.release();
                mock.allocator.destroy(completion);
            }
            return .disarm;
        };

        if (ud) |task| {
            const allocator = task.mock.allocator;
            defer allocator.destroy(completion);
            task.dispatch();
        }

        return .disarm;
    }

    // DeferredResponseTask delivers buffered responses asynchronously
    // This fixes the timing issue where responses were delivered before
    // the caller finished setting up request tracking
    const DeferredResponseTask = struct {
        mock: *Mock,
        request_id: u64,
        method: interface.LeanSupportedProtocol,
        responses: []interface.ReqRespResponse,
        error_response: ?struct { code: u32, message: []const u8 },

        fn dispatch(self: *DeferredResponseTask) void {
            const mock = self.mock;

            // Deliver error if present
            if (self.error_response) |err_resp| {
                mock.notifyError(self.request_id, self.method, err_resp.code, err_resp.message);
                // Free the copied message after notifyError (which makes its own copy)
                mock.allocator.free(@constCast(err_resp.message));
                mock.allocator.free(self.responses);
                mock.allocator.destroy(self);
                return;
            }

            // Deliver all buffered success responses
            // Note: notifySuccess takes ownership of the response via event.deinit(),
            // so we must NOT call resp.deinit() here to avoid double-free
            for (self.responses) |resp| {
                mock.notifySuccess(self.request_id, self.method, resp);
            }
            mock.allocator.free(self.responses);

            // Notify completion
            mock.notifyCompleted(self.request_id, self.method);

            mock.allocator.destroy(self);
        }
    };

    fn deferredResponseCallback(ud: ?*DeferredResponseTask, _: *xev.Loop, completion: *xev.Completion, r: xev.Timer.RunError!void) xev.CallbackAction {
        _ = r catch |err| {
            if (ud) |task| {
                const mock = task.mock;
                mock.logger.err("mock:: Deferred response scheduling failed: {any}", .{err});
                // Clean up responses
                for (task.responses) |*resp| {
                    resp.deinit();
                }
                mock.allocator.free(task.responses);
                // Free copied error message if present
                if (task.error_response) |err_resp| {
                    mock.allocator.free(@constCast(err_resp.message));
                }
                mock.allocator.destroy(task);
                mock.allocator.destroy(completion);
            }
            return .disarm;
        };

        if (ud) |task| {
            const allocator = task.mock.allocator;
            defer allocator.destroy(completion);
            task.dispatch();
        }

        return .disarm;
    }

    pub fn init(allocator: Allocator, loop: *xev.Loop, logger: zeam_utils.ModuleLogger, registry: ?*NodeNameRegistry) !Self {
        // Use provided registry or create empty one for backward compatibility
        const RegistryInfo = struct {
            registry: *NodeNameRegistry,
            owns_registry: bool,
        };

        const registry_info: RegistryInfo = if (registry) |reg| RegistryInfo{
            .registry = reg,
            .owns_registry = false,
        } else blk: {
            const empty_registry = try allocator.create(NodeNameRegistry);
            empty_registry.* = NodeNameRegistry.init(allocator);
            errdefer allocator.destroy(empty_registry);
            break :blk RegistryInfo{
                .registry = empty_registry,
                .owns_registry = true,
            };
        };

        const gossip_handler = try interface.GenericGossipHandler.init(allocator, loop, 0, logger, registry_info.registry);
        errdefer gossip_handler.deinit();

        const peer_event_handler = try interface.PeerEventHandler.init(allocator, 0, logger, registry_info.registry);
        errdefer peer_event_handler.deinit();

        const timer = try xev.Timer.init();
        errdefer timer.deinit();

        return Self{
            .allocator = allocator,
            .logger = logger,
            .gossipHandler = gossip_handler,
            .peerEventHandler = peer_event_handler,
            .registry = registry_info.registry,
            .owns_registry = registry_info.owns_registry,
            .rpcCallbacks = .empty,
            .peerLookup = .empty,
            .ownerToPeer = .empty,
            .peers = .empty,
            .connectedPairs = .empty,
            .activeStreams = .empty,
            .timer = timer,
            .nextPeerIndex = 0,
            .nextRequestId = 1,
            .force_publish_drop = false,
        };
    }

    /// Issue #808 review knob: toggle the simulated drop on every publish.
    pub fn setForcePublishDrop(self: *Self, drop: bool) void {
        self.force_publish_drop = drop;
    }

    pub fn deinit(self: *Self) void {
        var rpc_it = self.rpcCallbacks.iterator();
        while (rpc_it.next()) |entry| {
            var callback = entry.value_ptr.*;
            callback.deinit();
        }
        self.rpcCallbacks.deinit(self.allocator);

        self.peerLookup.deinit(self.allocator);
        self.ownerToPeer.deinit(self.allocator);
        self.connectedPairs.deinit(self.allocator);
        var stream_it = self.activeStreams.iterator();
        while (stream_it.next()) |entry| {
            self.allocator.destroy(entry.value_ptr.*);
        }
        self.activeStreams.deinit(self.allocator);

        self.timer.deinit();

        for (self.peers.items) |peer| {
            if (peer.peer_id) |pid| {
                self.allocator.free(pid);
            }
        }
        self.peers.deinit(self.allocator);

        self.gossipHandler.deinit();
        self.peerEventHandler.deinit();
        // Only destroy registry if we own it (created it ourselves)
        if (self.owns_registry) {
            self.registry.deinit();
            self.allocator.destroy(self.registry);
        }
    }

    fn allocateRequestId(self: *Self) u64 {
        const id = self.nextRequestId;
        self.nextRequestId +%= 1;
        if (self.nextRequestId == 0) {
            self.nextRequestId = 1;
        }
        return if (id == 0) self.allocateRequestId() else id;
    }

    fn getOrCreatePeerEntry(self: *Self, owner_ptr: *anyopaque) !struct { idx: usize, peer: *Peer } {
        const owner_key = @intFromPtr(owner_ptr);
        if (self.ownerToPeer.get(owner_key)) |idx| {
            return .{ .idx = idx, .peer = &self.peers.items[idx] };
        }

        const peer = Peer{ .owner_key = owner_key };
        try self.peers.append(self.allocator, peer);
        const idx = self.peers.items.len - 1;
        try self.ownerToPeer.put(self.allocator, owner_key, idx);
        return .{ .idx = idx, .peer = &self.peers.items[idx] };
    }

    fn assignPeerId(self: *Self, idx: usize) !void {
        var peer = &self.peers.items[idx];
        if (peer.peer_id != null) return;

        // Try to use meaningful peer IDs from registry if available
        // Map: node 0 -> "zeam_n1", node 1 -> "zeam_n2", etc.
        const peer_id = blk: {
            const node_names = [_][]const u8{ "zeam_n1", "zeam_n2", "zeam_n3", "zeam_n4" };
            if (self.nextPeerIndex < node_names.len) {
                const name = node_names[self.nextPeerIndex];
                break :blk try self.allocator.dupe(u8, name);
            }
            // Fallback to generic names for additional peers
            break :blk try std.fmt.allocPrint(self.allocator, "mock-peer-{d}", .{self.nextPeerIndex});
        };
        self.nextPeerIndex += 1;
        peer.peer_id = peer_id;
        try self.peerLookup.put(self.allocator, peer_id, idx);
    }

    fn ensurePeerEntry(self: *Self, peer_id: []const u8) !usize {
        if (self.peerLookup.get(peer_id)) |idx| {
            return idx;
        }

        const owned = try self.allocator.dupe(u8, peer_id);
        errdefer self.allocator.free(owned);

        const peer = Peer{
            .owner_key = 0,
            .peer_id = owned,
            .req_handler = null,
            .event_handler = null,
        };

        try self.peers.append(self.allocator, peer);
        const idx = self.peers.items.len - 1;
        errdefer {
            const new_len = self.peers.items.len - 1;
            const removed = self.peers.items[new_len];
            self.peers.shrinkRetainingCapacity(new_len);
            if (removed.peer_id) |pid| {
                self.allocator.free(pid);
            }
        }

        try self.peerLookup.put(self.allocator, owned, idx);
        return idx;
    }

    fn handleSyntheticRequest(self: *Self, request_id: u64, method: interface.LeanSupportedProtocol, request: *const interface.ReqRespRequest) void {
        const task = SyntheticResponseTask.init(self, request_id, method, request) catch |err| {
            self.logger.err("mock:: Failed to prepare synthetic response request_id={d}: {any}", .{ request_id, err });
            self.notifyError(request_id, method, 1, "mock peer has no block data");
            return;
        };

        const completion = self.allocator.create(xev.Completion) catch |err| {
            self.logger.err("mock:: Failed to allocate completion for synthetic response request_id={d}: {any}", .{ request_id, err });
            task.dispatch();
            return;
        };

        self.timer.run(
            self.gossipHandler.loop,
            completion,
            1,
            SyntheticResponseTask,
            task,
            syntheticResponseCallback,
        );
    }

    fn peerIsReady(self: *Self, idx: usize) bool {
        return self.peers.items[idx].isReady();
    }

    fn connectPair(self: *Self, idx_a: usize, idx_b: usize) void {
        if (idx_a == idx_b) return;

        const key = PairKey.from(idx_a, idx_b);
        if (self.connectedPairs.contains(key)) {
            return;
        }

        self.connectedPairs.put(self.allocator, key, {}) catch |err| {
            self.logger.err("mock:: Failed to track connected pair ({d}, {d}): {any}", .{ idx_a, idx_b, err });
            return;
        };

        const peer_a = &self.peers.items[idx_a];
        const peer_b = &self.peers.items[idx_b];

        const peer_a_id = peer_a.peer_id.?;
        const peer_b_id = peer_b.peer_id.?;

        // In mock, peer_a initiates (outbound) to peer_b, peer_b receives (inbound)
        peer_a.event_handler.?.onPeerConnected(peer_b_id, .outbound) catch |e| {
            self.logger.err("mock:: Failed delivering onPeerConnected to peer {s}: {any}", .{ peer_b_id, e });
        };

        peer_b.event_handler.?.onPeerConnected(peer_a_id, .inbound) catch |e| {
            self.logger.err("mock:: Failed delivering onPeerConnected to peer {s}: {any}", .{ peer_a_id, e });
        };
    }

    fn maybeConnectPeers(self: *Self, idx: usize) void {
        if (!self.peerIsReady(idx)) return;

        const peers_len = self.peers.items.len;
        var other_idx: usize = 0;
        while (other_idx < peers_len) : (other_idx += 1) {
            if (other_idx == idx) continue;
            if (!self.peerIsReady(other_idx)) continue;
            self.connectPair(idx, other_idx);
        }
    }

    fn cloneResponse(self: *Self, response: *const interface.ReqRespResponse) !interface.ReqRespResponse {
        return switch (response.*) {
            .status => |status_resp| interface.ReqRespResponse{ .status = status_resp },
            .blocks_by_root => |block_resp| blk: {
                var cloned_block: types.SignedBlock = undefined;
                try types.sszClone(self.allocator, types.SignedBlock, block_resp, &cloned_block);
                break :blk interface.ReqRespResponse{ .blocks_by_root = cloned_block };
            },
            .blocks_by_range => |block_resp| blk: {
                var cloned_block: types.SignedBlock = undefined;
                try types.sszClone(self.allocator, types.SignedBlock, block_resp, &cloned_block);
                break :blk interface.ReqRespResponse{ .blocks_by_range = cloned_block };
            },
        };
    }

    fn cloneRequest(self: *Self, request: *const interface.ReqRespRequest) !interface.ReqRespRequest {
        return switch (request.*) {
            .status => |status_req| interface.ReqRespRequest{ .status = status_req },
            .blocks_by_root => |block_req| blk: {
                var cloned_request: types.BlockByRootRequest = undefined;
                try types.sszClone(self.allocator, types.BlockByRootRequest, block_req, &cloned_request);
                break :blk interface.ReqRespRequest{ .blocks_by_root = cloned_request };
            },
            .blocks_by_range => |block_req| interface.ReqRespRequest{ .blocks_by_range = block_req },
        };
    }

    fn notifySuccess(self: *Self, request_id: u64, method: interface.LeanSupportedProtocol, response: interface.ReqRespResponse) void {
        var event = interface.ReqRespResponseEvent.initSuccess(request_id, method, response);
        defer event.deinit(self.allocator);

        if (self.rpcCallbacks.getPtr(request_id)) |callback| {
            callback.*.notify(&event) catch |notify_err| {
                self.logger.err("mock:: Failed delivering RPC success callback request_id={d}: {any}", .{ request_id, notify_err });
            };
        }
    }

    fn notifyError(self: *Self, request_id: u64, method: interface.LeanSupportedProtocol, code: u32, message: []const u8) void {
        const owned = self.allocator.dupe(u8, message) catch |alloc_err| {
            self.logger.err("mock:: Failed to allocate RPC error message for request_id={d}: {any}", .{ request_id, alloc_err });
            return;
        };

        var event = interface.ReqRespResponseEvent.initError(request_id, method, .{
            .code = code,
            .message = owned,
        });
        defer event.deinit(self.allocator);

        if (self.rpcCallbacks.fetchRemove(request_id)) |entry| {
            var callback = entry.value;
            callback.notify(&event) catch |notify_err| {
                self.logger.err("mock:: Failed delivering RPC error callback request_id={d}: {any}", .{ request_id, notify_err });
            };
            callback.deinit();
        } else {
            self.logger.warn("mock:: Dropping RPC error for unknown request_id={d}", .{request_id});
        }
    }

    fn notifyCompleted(self: *Self, request_id: u64, method: interface.LeanSupportedProtocol) void {
        var event = interface.ReqRespResponseEvent.initCompleted(request_id, method);
        defer event.deinit(self.allocator);

        if (self.rpcCallbacks.fetchRemove(request_id)) |entry| {
            var callback = entry.value;
            callback.notify(&event) catch |notify_err| {
                self.logger.err("mock:: Failed delivering RPC completion callback request_id={d}: {any}", .{ request_id, notify_err });
            };
            callback.deinit();
        }
    }

    fn serverStreamSendResponse(ptr: *anyopaque, response: *const interface.ReqRespResponse) anyerror!void {
        const ctx: *MockServerStream = @ptrCast(@alignCast(ptr));
        if (ctx.finished) {
            return StreamError.StreamAlreadyFinished;
        }

        // Buffer the response for async delivery instead of immediate notification
        // This fixes timing issues where responses arrive before request tracking is set up
        const cloned = try ctx.mock.cloneResponse(response);
        try ctx.buffered_responses.append(ctx.mock.allocator, cloned);
    }

    fn serverStreamSendError(ptr: *anyopaque, code: u32, message: []const u8) anyerror!void {
        const ctx: *MockServerStream = @ptrCast(@alignCast(ptr));
        if (ctx.finished) {
            return StreamError.StreamAlreadyFinished;
        }
        // Buffer the error for async delivery - copy message to avoid use-after-free
        // since the original message slice may be freed before deferred delivery
        const message_copy = try ctx.mock.allocator.dupe(u8, message);
        ctx.error_response = .{ .code = code, .message = message_copy };
        ctx.finished = true;
        // Schedule async delivery - don't finalize stream here, let the timer callback do it
        ctx.mock.scheduleDeferredResponse(ctx);
    }

    fn serverStreamFinish(ptr: *anyopaque) anyerror!void {
        const ctx: *MockServerStream = @ptrCast(@alignCast(ptr));
        if (ctx.finished) return;
        ctx.finished = true;
        // Schedule async delivery of buffered responses
        // This ensures the caller has finished setting up request tracking
        ctx.mock.scheduleDeferredResponse(ctx);
    }

    fn serverStreamIsFinished(ptr: *anyopaque) bool {
        const ctx: *MockServerStream = @ptrCast(@alignCast(ptr));
        return ctx.finished;
    }

    fn removeActiveStream(self: *Self, request_id: u64) void {
        if (self.activeStreams.fetchRemove(request_id)) |entry| {
            self.allocator.destroy(entry.value);
        }
    }

    fn finalizeServerStream(self: *Self, ctx: *MockServerStream) void {
        self.removeActiveStream(ctx.request_id);
    }

    fn scheduleDeferredResponse(self: *Self, ctx: *MockServerStream) void {
        // Create deferred task with buffered responses
        const task = self.allocator.create(DeferredResponseTask) catch |err| {
            self.logger.err("mock:: Failed to create deferred response task: {any}", .{err});
            // Clean up buffered responses on error
            for (ctx.buffered_responses.items) |*resp| {
                resp.deinit();
            }
            ctx.buffered_responses.deinit(self.allocator);
            self.finalizeServerStream(ctx);
            return;
        };

        // Transfer ownership of buffered responses to the task
        const responses = ctx.buffered_responses.toOwnedSlice(self.allocator) catch |err| {
            self.logger.err("mock:: Failed to transfer buffered responses: {any}", .{err});
            self.allocator.destroy(task);
            for (ctx.buffered_responses.items) |*resp| {
                resp.deinit();
            }
            ctx.buffered_responses.deinit(self.allocator);
            self.finalizeServerStream(ctx);
            return;
        };

        task.* = .{
            .mock = self,
            .request_id = ctx.request_id,
            .method = ctx.method,
            .responses = responses,
            .error_response = if (ctx.error_response) |err| .{ .code = err.code, .message = err.message } else null,
        };

        const completion = self.allocator.create(xev.Completion) catch |err| {
            self.logger.err("mock:: Failed to allocate completion for deferred response: {any}", .{err});
            // Dispatch immediately as fallback
            task.dispatch();
            self.finalizeServerStream(ctx);
            return;
        };

        // Schedule delivery with 1ms delay to allow caller to finish setup
        self.timer.run(
            self.gossipHandler.loop,
            completion,
            1,
            DeferredResponseTask,
            task,
            deferredResponseCallback,
        );

        // Finalize the stream (remove from active streams)
        self.finalizeServerStream(ctx);
    }

    pub fn publish(ptr: *anyopaque, data: *const interface.GossipMessage) anyerror!bool {
        // TODO: prevent from publishing to self handler
        const self: *Self = @ptrCast(@alignCast(ptr));
        // Issue #808: when the test harness toggles `force_publish_drop`, behave
        // like a real backend that dropped the publish (rust-libp2p command
        // channel full): no subscriber invocation, return `false` so the
        // caller exercises its drop-handling branch.
        if (self.force_publish_drop) {
            return false;
        }
        // Try to find a valid peer_id from connected peers, otherwise use a default
        const sender_peer_id = blk: {
            // Find first peer with a valid peer_id
            for (self.peers.items) |peer| {
                if (peer.peer_id) |pid| {
                    break :blk pid;
                }
            }
            // Fallback to default if no peers found
            break :blk "mock_publisher";
        };
        try self.gossipHandler.onGossip(data, sender_peer_id, true);
        // Mock backend has no command channel, so the publish always reaches
        // the local gossip handler synchronously.
        return true;
    }

    pub fn subscribe(ptr: *anyopaque, topics: []interface.GossipTopic, handler: interface.OnGossipCbHandler) anyerror!void {
        const self: *Self = @ptrCast(@alignCast(ptr));
        return self.gossipHandler.subscribe(topics, handler);
    }

    pub fn onGossip(ptr: *anyopaque, data: *const interface.GossipMessage, sender_peer_id: []const u8) anyerror!void {
        const self: *Self = @ptrCast(@alignCast(ptr));
        return self.gossipHandler.onGossip(data, sender_peer_id, true);
    }

    pub fn sendRequest(ptr: *anyopaque, peer_id: []const u8, req: *const interface.ReqRespRequest, callback: ?interface.OnReqRespResponseCbHandler) anyerror!u64 {
        const self: *Self = @ptrCast(@alignCast(ptr));

        const target_idx = try self.ensurePeerEntry(peer_id);
        const target_peer = &self.peers.items[target_idx];

        var request_copy = try self.cloneRequest(req);
        defer request_copy.deinit();

        const method = std.meta.activeTag(request_copy);
        const request_id = self.allocateRequestId();

        errdefer {
            if (self.rpcCallbacks.fetchRemove(request_id)) |entry| {
                var cb = entry.value;
                cb.deinit();
            }
        }

        const peer_id_copy = try self.allocator.dupe(u8, peer_id);
        errdefer self.allocator.free(peer_id_copy);
        const callback_entry = interface.ReqRespRequestCallback.init(method, self.allocator, callback, peer_id_copy);
        try self.rpcCallbacks.put(self.allocator, request_id, callback_entry);

        if (target_peer.req_handler) |handler| {
            const stream_ctx = try self.allocator.create(MockServerStream);
            stream_ctx.* = .{
                .mock = self,
                .request_id = request_id,
                .method = method,
                .sender_peer_id = peer_id,
            };

            var stream_registered = false;
            errdefer if (!stream_registered) self.allocator.destroy(stream_ctx);

            try self.activeStreams.put(self.allocator, request_id, stream_ctx);
            stream_registered = true;

            const stream_iface = interface.ReqRespServerStream{
                .ptr = stream_ctx,
                .sendResponseFn = serverStreamSendResponse,
                .sendErrorFn = serverStreamSendError,
                .finishFn = serverStreamFinish,
                .isFinishedFn = serverStreamIsFinished,
                .getPeerIdFn = mockStreamGetPeerId,
            };

            handler.onReqRespRequest(&request_copy, stream_iface) catch |err| {
                self.removeActiveStream(request_id);
                if (self.rpcCallbacks.fetchRemove(request_id)) |entry| {
                    var cb = entry.value;
                    cb.deinit();
                }
                return err;
            };
        } else {
            self.handleSyntheticRequest(request_id, method, &request_copy);
        }

        return request_id;
    }

    pub fn onReqRespRequest(ptr: *anyopaque, data: *interface.ReqRespRequest, stream: interface.ReqRespServerStream) anyerror!void {
        const self: *Self = @ptrCast(@alignCast(ptr));

        for (self.peers.items) |peer| {
            if (peer.req_handler) |handler| {
                try handler.onReqRespRequest(data, stream);
                return;
            }
        }

        return error.NoHandlerSubscribed;
    }

    pub fn subscribeReqResp(ptr: *anyopaque, handler: interface.OnReqRespRequestCbHandler) anyerror!void {
        const self: *Self = @ptrCast(@alignCast(ptr));
        const entry = try self.getOrCreatePeerEntry(handler.ptr);
        try self.assignPeerId(entry.idx);
        entry.peer.req_handler = handler;
        self.maybeConnectPeers(entry.idx);
    }

    pub fn subscribePeerEvents(ptr: *anyopaque, handler: interface.OnPeerEventCbHandler) anyerror!void {
        const self: *Self = @ptrCast(@alignCast(ptr));
        try self.peerEventHandler.subscribe(handler);

        const entry = try self.getOrCreatePeerEntry(handler.ptr);
        try self.assignPeerId(entry.idx);
        entry.peer.event_handler = handler;
        self.maybeConnectPeers(entry.idx);
    }

    pub fn getNetworkInterface(self: *Self) NetworkInterface {
        return .{
            .gossip = .{
                .ptr = self,
                .publishFn = publish,
                .subscribeFn = subscribe,
                .onGossipFn = onGossip,
            },
            .reqresp = .{
                .ptr = self,
                .sendRequestFn = sendRequest,
                .onReqRespRequestFn = onReqRespRequest,
                .subscribeFn = subscribeReqResp,
            },
            .peers = .{
                .ptr = self,
                .subscribeFn = subscribePeerEvents,
            },
        };
    }
};

/// Detect the best available I/O backend at runtime.
/// Factored out to avoid duplicating the detection snippet in every test.
fn detectBackendOrFail() !void {
    if (@hasDecl(xev, "detect")) {
        try xev.detect();
    }
}

test "Mock messaging across two subscribers" {
    const TestSubscriber = struct {
        calls: u32 = 0,
        received_message: ?interface.GossipMessage = null,

        fn onGossip(ptr: *anyopaque, message: *const interface.GossipMessage, sender_peer_id: []const u8) anyerror!void {
            _ = sender_peer_id;
            const self: *@This() = @ptrCast(@alignCast(ptr));
            self.calls += 1;
            self.received_message = message.*;
        }

        fn getCallbackHandler(self: *@This()) interface.OnGossipCbHandler {
            return .{
                .ptr = self,
                .onGossipCb = onGossip,
            };
        }
    };
    var arena_allocator = std.heap.ArenaAllocator.init(std.testing.allocator);
    defer arena_allocator.deinit();
    const allocator = arena_allocator.allocator();

    try detectBackendOrFail();
    var loop = try xev.Loop.init(.{});
    defer loop.deinit();

    var logger_config = zeam_utils.getTestLoggerConfig();
    const logger = logger_config.logger(.mock);
    var mock = try Mock.init(allocator, &loop, logger, null);

    // Create test subscribers with embedded data
    var subscriber1 = TestSubscriber{};
    var subscriber2 = TestSubscriber{};

    // Both subscribers subscribe to the same block topic using the complete network interface
    var topics = [_]interface.GossipTopic{.{ .kind = .block }};
    const network = mock.getNetworkInterface();
    try network.gossip.subscribe(&topics, subscriber1.getCallbackHandler());
    try network.gossip.subscribe(&topics, subscriber2.getCallbackHandler());

    // Create a simple block message
    var attestations = try types.AggregatedAttestations.init(allocator);

    const block_message = try allocator.create(interface.GossipMessage);
    defer allocator.destroy(block_message);
    block_message.* = .{ .block = .{
        .block = .{
            .slot = 1,
            .proposer_index = 0,
            .parent_root = [_]u8{1} ** 32,
            .state_root = [_]u8{2} ** 32,
            .body = .{
                .attestations = attestations,
            },
        },
        .signature = try types.createBlockSignatures(allocator, attestations.len()),
    } };

    // Publish the message using the network interface - both subscribers should receive it
    const published = try network.gossip.publish(block_message);
    try std.testing.expect(published);

    // Run the event loop to process scheduled callbacks
    try loop.run(.until_done);

    // Verify both subscribers received the message
    try std.testing.expect(subscriber1.calls == 1);
    try std.testing.expect(subscriber2.calls == 1);

    // Verify both subscribers received the same message content
    try std.testing.expect(subscriber1.received_message != null);
    try std.testing.expect(subscriber2.received_message != null);

    const received1 = subscriber1.received_message.?;
    const received2 = subscriber2.received_message.?;

    // Verify both received block messages
    try std.testing.expect(received1 == .block);
    try std.testing.expect(received2 == .block);

    // Verify the block content is identical
    try std.testing.expect(std.mem.eql(u8, &received1.block.block.parent_root, &received2.block.block.parent_root));
    try std.testing.expect(std.mem.eql(u8, &received1.block.block.state_root, &received2.block.block.state_root));
    try std.testing.expect(received1.block.block.slot == received2.block.block.slot);
    try std.testing.expect(received1.block.block.proposer_index == received2.block.block.proposer_index);

    // ---- Issue #808 review #2: force_publish_drop coverage ----
    // Reset subscriber call counters and toggle the drop knob: a subsequent
    // publish must return false and must NOT invoke any subscriber. This
    // gives the new `failed to publish … (backend dropped publish)` warn
    // arms in `Node.publishBlock` / `publishAttestation` / `publishAggregation`
    // an exercisable code path through the mock.
    subscriber1.calls = 0;
    subscriber2.calls = 0;
    mock.setForcePublishDrop(true);
    const dropped_publish = try network.gossip.publish(block_message);
    try std.testing.expect(!dropped_publish);
    try loop.run(.until_done);
    try std.testing.expect(subscriber1.calls == 0);
    try std.testing.expect(subscriber2.calls == 0);
    mock.setForcePublishDrop(false);
}

test "Mock status RPC between peers" {
    const TestPeer = struct {
        const Self = @This();
        allocator: Allocator,
        status: types.Status,
        connections: std.ArrayList([]u8) = .empty,
        received_status: ?types.Status = null,
        completed: bool = false,
        failures: u32 = 0,

        fn init(allocator: Allocator, status: types.Status) Self {
            return Self{ .allocator = allocator, .status = status };
        }

        fn deinit(self: *Self) void {
            for (self.connections.items) |conn| {
                self.allocator.free(conn);
            }
            self.connections.deinit(self.allocator);
        }

        fn onPeerConnected(ptr: *anyopaque, peer_id: []const u8, _: interface.PeerDirection) !void {
            const self: *Self = @ptrCast(@alignCast(ptr));
            const owned = try self.allocator.dupe(u8, peer_id);
            try self.connections.append(self.allocator, owned);
        }

        fn onPeerDisconnected(ptr: *anyopaque, peer_id: []const u8, _: interface.PeerDirection, _: interface.DisconnectionReason) !void {
            const self: *Self = @ptrCast(@alignCast(ptr));
            for (self.connections.items, 0..) |conn, idx| {
                if (std.mem.eql(u8, conn, peer_id)) {
                    const removed = self.connections.swapRemove(idx);
                    self.allocator.free(removed);
                    break;
                }
            }
        }

        fn onReqRespRequest(ptr: *anyopaque, request: *const interface.ReqRespRequest, stream: interface.ReqRespServerStream) anyerror!void {
            const self: *Self = @ptrCast(@alignCast(ptr));
            switch (request.*) {
                .status => {
                    var response = interface.ReqRespResponse{ .status = self.status };
                    try stream.sendResponse(&response);
                    try stream.finish();
                },
                .blocks_by_root => {
                    try stream.sendError(1, "unsupported");
                },
                .blocks_by_range => {
                    try stream.sendError(1, "unsupported");
                },
            }
        }

        fn onReqRespResponse(ptr: *anyopaque, event: *const interface.ReqRespResponseEvent) anyerror!void {
            const self: *Self = @ptrCast(@alignCast(ptr));
            switch (event.payload) {
                .success => |resp| switch (resp) {
                    .status => |status_resp| self.received_status = status_resp,
                    .blocks_by_root => {
                        self.failures += 1;
                    },
                    .blocks_by_range => {
                        self.failures += 1;
                    },
                },
                .failure => {
                    self.failures += 1;
                },
                .completed => {
                    self.completed = true;
                },
            }
        }

        fn getEventHandler(self: *Self) interface.OnPeerEventCbHandler {
            return .{
                .ptr = self,
                .onPeerConnectedCb = onPeerConnected,
                .onPeerDisconnectedCb = onPeerDisconnected,
            };
        }

        fn getReqHandler(self: *Self) interface.OnReqRespRequestCbHandler {
            return .{
                .ptr = self,
                .onReqRespRequestCb = onReqRespRequest,
            };
        }

        fn getResponseHandler(self: *Self) interface.OnReqRespResponseCbHandler {
            return .{
                .ptr = self,
                .onReqRespResponseCb = onReqRespResponse,
            };
        }
    };

    var arena_allocator = std.heap.ArenaAllocator.init(std.testing.allocator);
    defer arena_allocator.deinit();
    const allocator = arena_allocator.allocator();

    try detectBackendOrFail();
    var loop = try xev.Loop.init(.{});
    defer loop.deinit();

    var logger_config = zeam_utils.getTestLoggerConfig();
    const logger = logger_config.logger(.mock);

    var mock = try Mock.init(allocator, &loop, logger, null);
    defer mock.deinit();

    const backend_a = mock.getNetworkInterface();
    const backend_b = mock.getNetworkInterface();

    const status_a = types.Status{
        .finalized_root = [_]u8{0x01} ** 32,
        .finalized_slot = 10,
        .head_root = [_]u8{0x02} ** 32,
        .head_slot = 20,
    };
    const status_b = types.Status{
        .finalized_root = [_]u8{0xAA} ** 32,
        .finalized_slot = 30,
        .head_root = [_]u8{0xBB} ** 32,
        .head_slot = 40,
    };

    var peer_a = TestPeer.init(allocator, status_a);
    defer peer_a.deinit();
    var peer_b = TestPeer.init(allocator, status_b);
    defer peer_b.deinit();

    try backend_a.peers.subscribe(peer_a.getEventHandler());
    try backend_b.peers.subscribe(peer_b.getEventHandler());

    try backend_a.reqresp.subscribe(peer_a.getReqHandler());
    try backend_b.reqresp.subscribe(peer_b.getReqHandler());

    try std.testing.expectEqual(@as(usize, 1), peer_a.connections.items.len);
    try std.testing.expectEqual(@as(usize, 1), peer_b.connections.items.len);

    const remote_id_a = peer_a.connections.items[0];
    const response_handler_a = peer_a.getResponseHandler();
    var request = interface.ReqRespRequest{ .status = status_a };
    const request_id = try backend_a.reqresp.sendRequest(remote_id_a, &request, response_handler_a);
    request.deinit();

    try std.testing.expect(request_id != 0);

    // Run the event loop to process the async deferred response delivery
    try loop.run(.until_done);

    try std.testing.expect(peer_a.received_status != null);
    const received = peer_a.received_status.?;
    try std.testing.expect(std.mem.eql(u8, &received.finalized_root, &status_b.finalized_root));
    try std.testing.expectEqual(status_b.finalized_slot, received.finalized_slot);
    try std.testing.expect(std.mem.eql(u8, &received.head_root, &status_b.head_root));
    try std.testing.expectEqual(status_b.head_slot, received.head_slot);
    try std.testing.expect(peer_a.completed);
    try std.testing.expectEqual(@as(u32, 0), peer_a.failures);
}

// =====================================================================
// blocks_by_range mock-network roundtrip tests (PR #824 / issue #823)
// =====================================================================
//
// These tests exercise the wire-level contract of the new
// blocks_by_range RPC end-to-end through the mock network, without
// spinning up a full BeamNode. Each test sets up a TestPeer pair where
// peer_b (the responder) implements a hand-rolled fake server that
// plays a scripted slot→block sequence, and peer_a (the requester)
// drives `sendRequest` and accumulates the chunk events received via
// onReqRespResponse.
//
// What these tests pin:
//   * Multi-chunk delivery: M chunks in slot-ascending order, single
//     `completed` event at the end. Mirrors the spec contract for
//     blocks_by_range and the current chain.zig server-side response
//     order (finalized walk first, then unfinalized via forkchoice).
//   * Empty-stream-but-finish: server has nothing to send, just
//     `finish()` — peer sees `completed` with zero chunks, no error.
//     This is the start_slot > head.slot case Partha listed.
//   * RESOURCE_UNAVAILABLE error-path: server replies with code 3 +
//     message; peer sees the failure event, never a chunk, never a
//     bare `completed`. Pins the MIN_SLOTS_FOR_BLOCK_REQUESTS gate at
//     `node.zig:1196-1206`.
//   * Single-chunk happy path: just a sanity check that the
//     blocks_by_range payload variant flows through cloneResponse +
//     deferred delivery cleanly (no UAF on the SignedBlock interior).
//
// What these tests deliberately do NOT cover:
//   * Server-side range-walk logic (loadFinalizedSlotIndex,
//     forkchoice descendant walk, finalized/unfinalized boundary,
//     empty-slot skip, genesis-parent + self-parent loop guards) —
//     that lives in chain.zig and exercises a real DB / forkchoice;
//     better fit for a BeamNode-level integration test.
//   * Sync-trigger logic (gap > 64 → range vs head-by-root) — that
//     lives in onReqRespResponse's status arm at `node.zig:957-1000`
//     and needs a real chain to drive `getSyncStatus` decisions.
//   * Chunk-handler MissingPreState recovery in
//     `processBlockByRangeChunk` — same reason.
//
// Per @ch4r10t33r's review on PR #824: these mock tests close the
// "no dedicated unit test for the range RPC" gap on the WIRE
// contract; the BeamNode-level scenarios remain a follow-up.

fn buildSyntheticBlock(allocator: Allocator, slot: u64, parent_seed: u8) !types.SignedBlock {
    // Build a minimal synthetic SignedBlock for the mock test fixtures.
    // We don't run STF over these — the responder hands them back as
    // opaque payload, the requester just confirms the slot field
    // round-trips and the chunk sequence is correct.
    //
    // Construction uses the standard helpers from `pkgs/types/src/block.zig`
    // (`BeamBlock.setToDefault` + `createBlockSignatures`) so the
    // resulting SignedBlock is allocator-aware and `deinit()`-safe.
    // We then overwrite only the fields the tests actually inspect:
    // `slot` (asserted in the chunk-order check) and `parent_root` /
    // `state_root` (set deterministically from `parent_seed` so a
    // future test that wants to assert chunk identity has stable
    // bytes).
    var block: types.BeamBlock = undefined;
    try block.setToDefault(allocator);
    block.slot = slot;
    block.parent_root[0] = parent_seed;
    block.state_root[0] = parent_seed +% 1;

    const signatures = try types.createBlockSignatures(allocator, 0);
    return types.SignedBlock{
        .block = block,
        .signature = signatures,
    };
}

test "Mock blocks_by_range RPC: multi-chunk in slot-ascending order" {
    const TestPeer = struct {
        const Self = @This();
        allocator: Allocator,
        // Server-side: the slot sequence to emit (in order). Empty for clients.
        emit_slots: []const u64,
        // Client-side: accumulated chunks observed.
        received_slots: std.ArrayList(u64) = .empty,
        completed: bool = false,
        failures: u32 = 0,
        last_error_code: ?u32 = null,
        connections: std.ArrayList([]u8) = .empty,

        fn init(allocator: Allocator, emit_slots: []const u64) Self {
            return .{ .allocator = allocator, .emit_slots = emit_slots };
        }

        fn deinit(self: *Self) void {
            self.received_slots.deinit(self.allocator);
            for (self.connections.items) |conn| self.allocator.free(conn);
            self.connections.deinit(self.allocator);
        }

        fn onPeerConnected(ptr: *anyopaque, peer_id: []const u8, _: interface.PeerDirection) !void {
            const self: *Self = @ptrCast(@alignCast(ptr));
            const owned = try self.allocator.dupe(u8, peer_id);
            try self.connections.append(self.allocator, owned);
        }

        fn onPeerDisconnected(ptr: *anyopaque, peer_id: []const u8, _: interface.PeerDirection, _: interface.DisconnectionReason) !void {
            const self: *Self = @ptrCast(@alignCast(ptr));
            for (self.connections.items, 0..) |conn, idx| {
                if (std.mem.eql(u8, conn, peer_id)) {
                    const removed = self.connections.swapRemove(idx);
                    self.allocator.free(removed);
                    break;
                }
            }
        }

        fn onReqRespRequest(ptr: *anyopaque, request: *const interface.ReqRespRequest, stream: interface.ReqRespServerStream) anyerror!void {
            const self: *Self = @ptrCast(@alignCast(ptr));
            switch (request.*) {
                .blocks_by_range => {
                    // Emit one synthetic block per slot in `emit_slots`,
                    // in order. The block lifetime ends with `sendResponse`
                    // (which clones into the deferred buffer); we deinit
                    // the local copy after each call.
                    for (self.emit_slots, 0..) |slot, i| {
                        var block = try buildSyntheticBlock(self.allocator, slot, @as(u8, @intCast(i + 1)));
                        defer block.deinit();
                        var response = interface.ReqRespResponse{ .blocks_by_range = block };
                        try stream.sendResponse(&response);
                    }
                    try stream.finish();
                },
                .status, .blocks_by_root => {
                    try stream.sendError(1, "test peer only handles blocks_by_range");
                },
            }
        }

        fn onReqRespResponse(ptr: *anyopaque, event: *const interface.ReqRespResponseEvent) anyerror!void {
            const self: *Self = @ptrCast(@alignCast(ptr));
            switch (event.payload) {
                .success => |resp| switch (resp) {
                    .blocks_by_range => |signed_block| {
                        try self.received_slots.append(self.allocator, signed_block.block.slot);
                    },
                    else => self.failures += 1,
                },
                .failure => |fail| {
                    self.failures += 1;
                    self.last_error_code = fail.code;
                },
                .completed => self.completed = true,
            }
        }

        fn getEventHandler(self: *Self) interface.OnPeerEventCbHandler {
            return .{ .ptr = self, .onPeerConnectedCb = onPeerConnected, .onPeerDisconnectedCb = onPeerDisconnected };
        }
        fn getReqHandler(self: *Self) interface.OnReqRespRequestCbHandler {
            return .{ .ptr = self, .onReqRespRequestCb = onReqRespRequest };
        }
        fn getResponseHandler(self: *Self) interface.OnReqRespResponseCbHandler {
            return .{ .ptr = self, .onReqRespResponseCb = onReqRespResponse };
        }
    };

    var arena_allocator = std.heap.ArenaAllocator.init(std.testing.allocator);
    defer arena_allocator.deinit();
    const allocator = arena_allocator.allocator();

    try detectBackendOrFail();
    var loop = try xev.Loop.init(.{});
    defer loop.deinit();

    var logger_config = zeam_utils.getTestLoggerConfig();
    const logger = logger_config.logger(.mock);

    var mock = try Mock.init(allocator, &loop, logger, null);
    defer mock.deinit();

    const backend_a = mock.getNetworkInterface();
    const backend_b = mock.getNetworkInterface();

    // Server emits 3 chunks at slots 7, 8, 9 — strictly ascending.
    const emit_slots = [_]u64{ 7, 8, 9 };
    var peer_a = TestPeer.init(allocator, &.{}); // requester
    defer peer_a.deinit();
    var peer_b = TestPeer.init(allocator, &emit_slots); // responder
    defer peer_b.deinit();

    try backend_a.peers.subscribe(peer_a.getEventHandler());
    try backend_b.peers.subscribe(peer_b.getEventHandler());
    try backend_a.reqresp.subscribe(peer_a.getReqHandler());
    try backend_b.reqresp.subscribe(peer_b.getReqHandler());

    try std.testing.expectEqual(@as(usize, 1), peer_a.connections.items.len);

    const remote = peer_a.connections.items[0];
    var request = interface.ReqRespRequest{ .blocks_by_range = .{ .start_slot = 7, .count = 3 } };
    const request_id = try backend_a.reqresp.sendRequest(remote, &request, peer_a.getResponseHandler());
    request.deinit();
    try std.testing.expect(request_id != 0);

    try loop.run(.until_done);

    // Assert: 3 chunks received in [7, 8, 9] order; one completed; no failures.
    try std.testing.expectEqual(@as(usize, 3), peer_a.received_slots.items.len);
    try std.testing.expectEqual(@as(u64, 7), peer_a.received_slots.items[0]);
    try std.testing.expectEqual(@as(u64, 8), peer_a.received_slots.items[1]);
    try std.testing.expectEqual(@as(u64, 9), peer_a.received_slots.items[2]);
    try std.testing.expect(peer_a.completed);
    try std.testing.expectEqual(@as(u32, 0), peer_a.failures);
}

test "Mock blocks_by_range RPC: empty stream + clean finish (start_slot past head)" {
    // Pins the start_slot > head.slot path Partha listed. The server
    // has nothing to emit (empty slot list) and immediately calls
    // `finish()`. Peer should observe zero chunks + a single `completed`
    // event, no error.
    const TestPeer = struct {
        const Self = @This();
        allocator: Allocator,
        received_slots: std.ArrayList(u64) = .empty,
        completed: bool = false,
        failures: u32 = 0,
        connections: std.ArrayList([]u8) = .empty,

        fn init(allocator: Allocator) Self {
            return .{ .allocator = allocator };
        }
        fn deinit(self: *Self) void {
            self.received_slots.deinit(self.allocator);
            for (self.connections.items) |conn| self.allocator.free(conn);
            self.connections.deinit(self.allocator);
        }
        fn onPeerConnected(ptr: *anyopaque, peer_id: []const u8, _: interface.PeerDirection) !void {
            const self: *Self = @ptrCast(@alignCast(ptr));
            try self.connections.append(self.allocator, try self.allocator.dupe(u8, peer_id));
        }
        fn onPeerDisconnected(ptr: *anyopaque, peer_id: []const u8, _: interface.PeerDirection, _: interface.DisconnectionReason) !void {
            const self: *Self = @ptrCast(@alignCast(ptr));
            for (self.connections.items, 0..) |conn, idx| {
                if (std.mem.eql(u8, conn, peer_id)) {
                    const removed = self.connections.swapRemove(idx);
                    self.allocator.free(removed);
                    break;
                }
            }
        }
        fn onReqRespRequestEmpty(ptr: *anyopaque, request: *const interface.ReqRespRequest, stream: interface.ReqRespServerStream) anyerror!void {
            _ = ptr;
            switch (request.*) {
                .blocks_by_range => try stream.finish(), // no chunks, just close
                else => try stream.sendError(1, "unsupported"),
            }
        }
        fn onReqRespResponse(ptr: *anyopaque, event: *const interface.ReqRespResponseEvent) anyerror!void {
            const self: *Self = @ptrCast(@alignCast(ptr));
            switch (event.payload) {
                .success => |resp| switch (resp) {
                    .blocks_by_range => |signed_block| try self.received_slots.append(self.allocator, signed_block.block.slot),
                    else => self.failures += 1,
                },
                .failure => self.failures += 1,
                .completed => self.completed = true,
            }
        }
        fn getEventHandler(self: *Self) interface.OnPeerEventCbHandler {
            return .{ .ptr = self, .onPeerConnectedCb = onPeerConnected, .onPeerDisconnectedCb = onPeerDisconnected };
        }
        fn getReqHandler(self: *Self) interface.OnReqRespRequestCbHandler {
            return .{ .ptr = self, .onReqRespRequestCb = onReqRespRequestEmpty };
        }
        fn getResponseHandler(self: *Self) interface.OnReqRespResponseCbHandler {
            return .{ .ptr = self, .onReqRespResponseCb = onReqRespResponse };
        }
    };

    var arena_allocator = std.heap.ArenaAllocator.init(std.testing.allocator);
    defer arena_allocator.deinit();
    const allocator = arena_allocator.allocator();

    try detectBackendOrFail();
    var loop = try xev.Loop.init(.{});
    defer loop.deinit();

    var logger_config = zeam_utils.getTestLoggerConfig();
    const logger = logger_config.logger(.mock);
    var mock = try Mock.init(allocator, &loop, logger, null);
    defer mock.deinit();

    const backend_a = mock.getNetworkInterface();
    const backend_b = mock.getNetworkInterface();

    var peer_a = TestPeer.init(allocator);
    defer peer_a.deinit();
    var peer_b = TestPeer.init(allocator);
    defer peer_b.deinit();

    try backend_a.peers.subscribe(peer_a.getEventHandler());
    try backend_b.peers.subscribe(peer_b.getEventHandler());
    try backend_a.reqresp.subscribe(peer_a.getReqHandler());
    try backend_b.reqresp.subscribe(peer_b.getReqHandler());

    const remote = peer_a.connections.items[0];
    // start_slot 9999 > any plausible head — server replies with an
    // empty stream + finish.
    var request = interface.ReqRespRequest{ .blocks_by_range = .{ .start_slot = 9999, .count = 5 } };
    _ = try backend_a.reqresp.sendRequest(remote, &request, peer_a.getResponseHandler());
    request.deinit();

    try loop.run(.until_done);

    try std.testing.expectEqual(@as(usize, 0), peer_a.received_slots.items.len);
    try std.testing.expect(peer_a.completed);
    try std.testing.expectEqual(@as(u32, 0), peer_a.failures);
}

test "Mock blocks_by_range RPC: RESOURCE_UNAVAILABLE error path (history window)" {
    // Pins the MIN_SLOTS_FOR_BLOCK_REQUESTS gate at
    // node.zig:1196-1206. Server replies with code
    // RPC_ERR_RESOURCE_UNAVAILABLE (3) + message; peer should observe
    // a `failure` event with that code, NOT a bare `completed`, and
    // never any chunk.
    const TestPeer = struct {
        const Self = @This();
        allocator: Allocator,
        received_slots: std.ArrayList(u64) = .empty,
        completed: bool = false,
        failures: u32 = 0,
        last_error_code: ?u32 = null,
        connections: std.ArrayList([]u8) = .empty,

        fn init(allocator: Allocator) Self {
            return .{ .allocator = allocator };
        }
        fn deinit(self: *Self) void {
            self.received_slots.deinit(self.allocator);
            for (self.connections.items) |conn| self.allocator.free(conn);
            self.connections.deinit(self.allocator);
        }
        fn onPeerConnected(ptr: *anyopaque, peer_id: []const u8, _: interface.PeerDirection) !void {
            const self: *Self = @ptrCast(@alignCast(ptr));
            try self.connections.append(self.allocator, try self.allocator.dupe(u8, peer_id));
        }
        fn onPeerDisconnected(ptr: *anyopaque, peer_id: []const u8, _: interface.PeerDirection, _: interface.DisconnectionReason) !void {
            const self: *Self = @ptrCast(@alignCast(ptr));
            for (self.connections.items, 0..) |conn, idx| {
                if (std.mem.eql(u8, conn, peer_id)) {
                    const removed = self.connections.swapRemove(idx);
                    self.allocator.free(removed);
                    break;
                }
            }
        }
        fn onReqRespRequest(ptr: *anyopaque, request: *const interface.ReqRespRequest, stream: interface.ReqRespServerStream) anyerror!void {
            _ = ptr;
            switch (request.*) {
                .blocks_by_range => try stream.sendError(3, "requested range is outside history window"),
                else => try stream.sendError(1, "unsupported"),
            }
        }
        fn onReqRespResponse(ptr: *anyopaque, event: *const interface.ReqRespResponseEvent) anyerror!void {
            const self: *Self = @ptrCast(@alignCast(ptr));
            switch (event.payload) {
                .success => |resp| switch (resp) {
                    .blocks_by_range => |signed_block| try self.received_slots.append(self.allocator, signed_block.block.slot),
                    else => self.failures += 1,
                },
                .failure => |fail| {
                    self.failures += 1;
                    self.last_error_code = fail.code;
                },
                .completed => self.completed = true,
            }
        }
        fn getEventHandler(self: *Self) interface.OnPeerEventCbHandler {
            return .{ .ptr = self, .onPeerConnectedCb = onPeerConnected, .onPeerDisconnectedCb = onPeerDisconnected };
        }
        fn getReqHandler(self: *Self) interface.OnReqRespRequestCbHandler {
            return .{ .ptr = self, .onReqRespRequestCb = onReqRespRequest };
        }
        fn getResponseHandler(self: *Self) interface.OnReqRespResponseCbHandler {
            return .{ .ptr = self, .onReqRespResponseCb = onReqRespResponse };
        }
    };

    var arena_allocator = std.heap.ArenaAllocator.init(std.testing.allocator);
    defer arena_allocator.deinit();
    const allocator = arena_allocator.allocator();

    try detectBackendOrFail();
    var loop = try xev.Loop.init(.{});
    defer loop.deinit();

    var logger_config = zeam_utils.getTestLoggerConfig();
    const logger = logger_config.logger(.mock);
    var mock = try Mock.init(allocator, &loop, logger, null);
    defer mock.deinit();

    const backend_a = mock.getNetworkInterface();
    const backend_b = mock.getNetworkInterface();

    var peer_a = TestPeer.init(allocator);
    defer peer_a.deinit();
    var peer_b = TestPeer.init(allocator);
    defer peer_b.deinit();

    try backend_a.peers.subscribe(peer_a.getEventHandler());
    try backend_b.peers.subscribe(peer_b.getEventHandler());
    try backend_a.reqresp.subscribe(peer_a.getReqHandler());
    try backend_b.reqresp.subscribe(peer_b.getReqHandler());

    const remote = peer_a.connections.items[0];
    // start_slot 1 against a "deep chain" — server replies with code 3.
    var request = interface.ReqRespRequest{ .blocks_by_range = .{ .start_slot = 1, .count = 64 } };
    _ = try backend_a.reqresp.sendRequest(remote, &request, peer_a.getResponseHandler());
    request.deinit();

    try loop.run(.until_done);

    // No chunks. One failure with code 3. No bare `completed`.
    try std.testing.expectEqual(@as(usize, 0), peer_a.received_slots.items.len);
    try std.testing.expectEqual(@as(u32, 1), peer_a.failures);
    try std.testing.expect(peer_a.last_error_code != null);
    try std.testing.expectEqual(@as(u32, 3), peer_a.last_error_code.?);
    try std.testing.expect(!peer_a.completed);
}
